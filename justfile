set dotenv-load := true
set positional-arguments := true

DATETIME := `TZ=UTC date '+%Y-%m-%d %H:%M:%S'`
BLANK_STAGING := env('BLANK_STAGING', 'example')
BLANK_DOCKER_TAG := env('BLANK_DOCKER_TAG', 'blank/default')
BLANK_BASE_IMAGE := env('BLANK_BASE_IMAGE', 'python:3.12-slim-bookworm')

UV_INDEX_PRIVATE_USERNAME := env('UV_INDEX_PRIVATE_USERNAME', '__token__')
UV_INDEX_PRIVATE_PASSWORD := env('UV_INDEX_PRIVATE_PASSWORD', '')

default:
    @just --list

# (re)create clean and full-featured development environment from scratch with all required tools and packages
[group('environment')]
install:
    @mise trust --yes mise.toml
    @mise install

# run pre-commit hooks on all files, include lint, check ant pre-commit hooks
[group('maintenance')]
pre-commit: (_development_packages)
    @uv run --quiet \
    pre-commit run \
        --config etc/pre-commit.yaml \
        --all

# in-place autoformat sourcetree, destructive (!)
[group('maintenance')]
lint: (_development_packages)
    @echo "check dependencies state is actual and locked.."
    @uv lock --check --quiet

    @echo "force format by black.."
    @uv run --quiet \
    black \
        --quiet \
        --line-length 79 \
        --target-version py312 \
        'app/' 'src/' 'tests/'

    @echo "force format by ruff.."
    @uv run --quiet \
    ruff check \
        --fix \
        --quiet \
        'app/' 'src/' 'tests/'

    @echo "force yaml format.."
    @uv run --quiet \
    yamlfix \
        --config-file etc/lint/yamlfix.toml \
        --exclude '.cache/**/*.yml' \
        --exclude '.cache/**/*.yaml' \
        --exclude '.venv/**/*.yml' \
        --exclude '.venv/**/*.yaml' \
        .

    @echo "check by yaml lint.."
    @uv run --quiet \
    yamllint \
        --format parsable \
        --config-file etc/lint/yamllint.yaml \
        .

# non-destructive source files check, fast fail on errors
[group('maintenance')]
check: (_development_packages)
    @echo "check ruff.. (general linting).."
    @uv run --quiet \
    ruff check \
        --quiet \
        'app/' 'src/' 'tests/'

    @echo "check mypy.. (static types check).."
    @uv run --quiet \
    mypy \
        --cache-fine-grained \
        --config etc/lint/mypy.toml \
        'app/' 'src/' 'tests/'

    @echo "check vulture.. (unused code detection).."
    @uv run --quiet \
    vulture \
        --min-confidence 66 \
        'app/' 'src/'

    @echo "check pyright.. (consistency check).."
    @uv run --quiet \
    basedpyright \
        --project etc/lint/pyright.json \
        'app/' 'src/' 'tests/'

    @echo "check refurb (bad patterns usage).."
    @uv run --quiet \
    refurb \
        --quiet \
        --enable-all \
        --python-version 3.12 \
        --config-file pyproject.toml \
        --sort filename \
        'app/' 'src/' 'tests/'

    @echo "check bandit (simple sources security audit).."
    @uv run --quiet \
    bandit \
        --quiet \
        --recursive \
        --severity-level all \
        --confidence-level all \
        --configfile pyproject.toml \
        'app/' 'src/' 'tests/'

    @echo "check hexora (malware security audit).."
    @uv run --quiet hexora audit 'app/'
    @uv run --quiet hexora audit 'src/'

# make new database migration with autogenerated changes
[group('maintenance')]
migration description='':
    @uv run --quiet \
    alembic revision \
        --autogenerate \
        --message \
        "{{ if description == '' {DATETIME} else {description} }}"

# apply database migrations
[group('maintenance')]
migrate:
    @uv run --quiet \
    alembic upgrade head

# tests per se tests, with coverage report
[group('maintenance')]
test:
    @PYTHONASYNCIODEBUG=1 \
    uv run --quiet \
    pytest \
        -rs \
        -svvv \
        --cov src \
        --cov-report term-missing

# clean bytecode and cache files, also useful to reset state of linters and type checkers
[group('maintenance')]
clean:
    @rm -rf .*_cache/
    @find . -type d -name "__pycache__" -exec rm -rf {} +
    @find . -type f -name "*.pyc" -exec rm -f {} +

# upgrade dependencies graph
[group('packaging')]
upgrade:
    @echo "upgrading mise dependencies.."
    @mise upgrade \
        --quiet \
        --yes \

    @echo "updating pre-commit hooks.."
    @uv run --quiet \
    pre-commit autoupdate \
        --config etc/pre-commit.yaml

    @echo "upgrading project (with development group) dependencies.."
    @uv sync \
        --refresh \
        --upgrade \
        --group development

    @echo "installed dependencies after upgrade:"
    @uv pip list

    @echo "locking updated dependencies.."
    @uv lock --upgrade

# build docker image
[group('packaging')]
dock:
    @uv lock --quiet
    docker buildx build --load \
        --build-arg BLANK_BASE_IMAGE="{{BLANK_BASE_IMAGE}}" \
        --build-arg UV_INDEX_PRIVATE_USERNAME="{{UV_INDEX_PRIVATE_USERNAME}}" \
        --build-arg UV_INDEX_PRIVATE_PASSWORD="{{UV_INDEX_PRIVATE_PASSWORD}}" \
        -t "{{BLANK_DOCKER_TAG}}" .

# production method of running the application
[group('project')]
run:
    @uv run --quiet app/main.py

# development method of running the application
[group('project')]
develop:
    @uv run app/main.py

# forward ports from remote environment using kubefwd; don't forgot run `gcloud auth login`
[group('project')]
ports:
    @echo " -> forward ports to {{BLANK_STAGING}} environment"
    @sudo kubefwd svc -n {{BLANK_STAGING}}

# add development packages to environment when skipped
[group('maintenance')]
_development_packages:
    @uv sync --quiet --group development
